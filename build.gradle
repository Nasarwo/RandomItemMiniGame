plugins {
	id "java"
}

version = "1.2.2"
group = "net.dagger.randomitemminigame"

base.archivesName = "plugin"

def toolsDir = layout.projectDirectory.dir("build/tools")
def runDir = layout.projectDirectory.dir("run")
def pluginsDir = runDir.dir("plugins")
def paperJar = runDir.file("paper.jar")
def paperVersion = "1.21.11"

repositories {
	mavenCentral()
	maven {
		name = "papermc-repo"
		url = "https://repo.papermc.io/repository/maven-public/"
	}
	maven {
		name = "dmulloy2-repo"
		url = "https://repo.dmulloy2.net/repository/public/"
	}
}

dependencies {
	compileOnly("io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT")
}


def javaVersion = JavaVersion.VERSION_21

java {
	sourceCompatibility = javaVersion
	targetCompatibility = javaVersion
	toolchain.languageVersion = JavaLanguageVersion.of(javaVersion.toString())
}

tasks.withType(JavaCompile).configureEach {
	options.release = javaVersion.toString() as Integer
	options.compilerArgs += ['-Xlint:-removal']
}

tasks.register('prepareDirs') {
	group 'paper'
	doLast {
		toolsDir.asFile.mkdirs()
		pluginsDir.asFile.mkdirs()
	}
}

tasks.register('downloadPaper') {
	group 'paper'
	dependsOn 'prepareDirs'

	doLast {
		if (!paperJar.asFile.exists()) {
			def apiUrl = "https://api.papermc.io/v2/projects/paper/versions/${paperVersion}"
			def versionInfo = new groovy.json.JsonSlurper().parse(new URL(apiUrl))
			def build = versionInfo.builds[-1]
			def downloadUrl = "https://api.papermc.io/v2/projects/paper/versions/${paperVersion}/builds/${build}/downloads/paper-${paperVersion}-${build}.jar"
			new URL(downloadUrl).withInputStream { i ->
				paperJar.asFile.withOutputStream { it << i }
			}
		}
	}
}

tasks.register('buildPaper', Exec) {
	group 'paper'
	dependsOn tasks.downloadPaper

	workingDir = runDir.asFile
	commandLine = ["java", "-jar", paperJar.asFile.absolutePath, "--rev", paperVersion]
	onlyIf { !paperJar.asFile.exists() }
}

tasks.register('copyPlugin', Copy) {
	group 'paper'
	dependsOn tasks.named('jar')

	from layout.buildDirectory.file("libs/plugin.jar")
	into layout.projectDirectory.dir("run/plugins")
}

tasks.register('runPaperServer', Exec) {
	group 'paper'
	dependsOn tasks.build
	dependsOn tasks.copyPlugin

	workingDir = file("run")
	commandLine = ["java", "-jar", paperJar] as List<String>
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

processResources {
	def props = [version: version]
	inputs.properties props
	filteringCharset 'UTF-8'
	filesMatching('plugin.yml') {
		expand props
	}
}
